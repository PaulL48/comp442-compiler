<START> ::= <Prog>
<Prog> ::= <ClassDecl> <FuncDef> 'main' <FuncBody>

<AParamsStart> ::= @makenode~list~aParams@ <AParams>

<AParams> ::= <Expr> <AParamsTail>
<AParams> ::= EPSILON

<AParamsTail> ::= ',' <Expr> <AParamsTail>
<AParamsTail> ::= EPSILON

<AddOp> ::= '+' @makenode~string~+@
<AddOp> ::= '-' @makenode~string~-@
<AddOp> ::= 'or' @makenode~string~or@

<ArithExpr> ::= <Term> <ArithExprTail>

<ArithExprTail> ::= <AddOp> <Term> <ArithExprTail>
<ArithExprTail> ::= EPSILON

<ArraySizeRept> ::= '[' <IntNum> ']' <ArraySizeRept>
<ArraySizeRept> ::= EPSILON

<AssignOp> ::= 'assign' @makenode~string~assign@

<AssignStatTail> ::= <AssignOp> <Expr>

<ClassDecl> ::= 'class' 'id' @makenode~string~id@ <Inherit> '{' <ClassDeclBody> '}' ';' <ClassDecl>
<ClassDecl> ::= EPSILON

<ClassDeclBody> ::= <Visibility> <MemberDecl> <ClassDeclBody>
<ClassDeclBody> ::= EPSILON

<ClassMethod> ::= 'sr' 'id' @makenode~string~scopeSpec@
<ClassMethod> ::= EPSILON @makenode~epsilon~scopeSpec@

<Expr> ::= <ArithExpr> <ExprTail>

<ExprTail> ::= <RelOp> <ArithExpr>
<ExprTail> ::= EPSILON

<FParams> ::= <Type> 'id' @makenode~string~id@ @makenode~list~dimlist@ <ArraySizeRept> @makefamily~3~fparam@ @makesibling@ <FParamsTail>
<FParams> ::= EPSILON

<FParamsTail> ::= ',' <Type> 'id' @makenode~string~id@ @makenode~list~dimlist@ <ArraySizeRept> @makefamily~3~fparam@ @makesibling@ <FParamsTail>
<FParamsTail> ::= EPSILON

<Factor> ::= <FuncOrVar>
<Factor> ::= 'intnum' @makenode~integer~factor@
<Factor> ::= 'floatnum' @makenode~float~factor@
<Factor> ::= 'stringlit' @makenode~string~factor@
<Factor> ::= '(' <Expr> ')'
<Factor> ::= 'not' <Factor>
<Factor> ::= <Sign> <Factor>
<Factor> ::= 'qm' '[' <Expr> ':' <Expr> ':' <Expr> ']'

<FuncBody> ::= '{' <MethodBodyVar> @makenode~list~statBlock@ <StatementList> '}' @makefamily~2~funcBody@

<FuncDecl> ::= 'func' 'id' @makenode~string~id@ '(' @makenode~list~fparamList@ <FParams> ')' ':' <FuncDeclTail> ';'

<FuncDeclTail> ::= <Type>
<FuncDeclTail> ::= 'void' @makenode~epsilon~type@

<FuncDef> ::= <Function> <FuncDef>
<FuncDef> ::= EPSILON

<FuncHead> ::= 'func' 'id' @makenode~string~id@ <ClassMethod> '(' @makenode~list~fparamList@ <FParams> ')' ':' <FuncDeclTail>

<FuncOrAssignStat> ::= 'id' @makenode~string~id@ <FuncOrAssignStatIdnest>

<FuncOrAssignStatIdnest> ::= <IndiceRepStart> <FuncOrAssignStatIdnestVarTail>
<FuncOrAssignStatIdnest> ::= '(' <AParamsStart> ')' <FuncOrAssignStatIdnestFuncTail>

<FuncOrAssignStatIdnestFuncTail> ::= '.' 'id' @makenode~string~id@ <FuncStatTail>
<FuncOrAssignStatIdnestFuncTail> ::= EPSILON

<FuncStatTail> ::= <IndiceRepStart> '.' 'id' @makenode~string~id@ <FuncStatTail>
<FuncStatTail> ::= '(' <AParamsStart> ')' <FuncStatTailIdnest>

<FuncStatTailIdnest> ::= '.' 'id' @makenode~string~id@ <FuncStatTail>
<FuncStatTailIdnest> ::= EPSILON

<FuncOrAssignStatIdnestVarTail> ::= '.' 'id' @makenode~string~id@ <FuncOrAssignStatIdnest>
<FuncOrAssignStatIdnestVarTail> ::= <AssignStatTail>

<FuncOrVar> ::= 'id' @makenode~string~id@ <FuncOrVarIdnest>

<FuncOrVarIdnest> ::= <IndiceRepStart> <FuncOrVarIdnestTail>
<FuncOrVarIdnest> ::= '(' <AParamsStart> ')' <FuncOrVarIdnestTail>

<FuncOrVarIdnestTail> ::= '.' 'id' @makenode~string~id@ <FuncOrVarIdnest>
<FuncOrVarIdnestTail> ::= EPSILON

<Function> ::= <FuncHead> <FuncBody> @makefamily~5~funcDef@

<IndiceRepStart> ::= @makenode~list~indices@ <IndiceRep>

<IndiceRep> ::= '[' <Expr> ']' <IndiceRep>
<IndiceRep> ::= EPSILON

<Inherit> ::= 'inherits' 'id' @makenode~string~id@ <NestedId>
<Inherit> ::= EPSILON

<IntNum> ::= 'intnum' @makenode~integer~num@ @makesibling@
<IntNum> ::= EPSILON

<MemberDecl> ::= <FuncDecl>
<MemberDecl> ::= <VarDecl>

<MethodBodyVar> ::= 'var' '{' @makenode~list~varList@ <VarDeclRep> '}'
<MethodBodyVar> ::= EPSILON @makenode~epsilon~varList@

<MultOp> ::= '*' @makenode~string~*@
<MultOp> ::= '/' @makenode~string~/@
<MultOp> ::= 'and' @makenode~string~and@

<NestedId> ::= ',' 'id' @makenode~string~id@ <NestedId>
<NestedId> ::= EPSILON


<RelOp> ::= 'eq'
<RelOp> ::= 'neq'
<RelOp> ::= 'lt'
<RelOp> ::= 'gt'
<RelOp> ::= 'leq'
<RelOp> ::= 'geq'


<Sign> ::= '+'
<Sign> ::= '-'

<StatBlock> ::= '{' <StatementList> '}'
<StatBlock> ::= <Statement>
<StatBlock> ::= EPSILON

<Statement> ::= <FuncOrAssignStat> ';'
<Statement> ::= 'if' '(' <Expr> ')' 'then' <StatBlock> 'else' <StatBlock> ';'
<Statement> ::= 'while' '(' <Expr> ')' <StatBlock> ';'
<Statement> ::= 'read' '(' <Variable> ')' ';'
<Statement> ::= 'write' '(' <Expr> ')' ';'
<Statement> ::= 'return' '(' <Expr> ')' ';'
<Statement> ::= 'break' @makenode~string~breakStat@ ';'
<Statement> ::= 'continue' @makenode~string~continueStat@ ';'

<StatementList> ::= <Statement> @makesibling@ <StatementList>
<StatementList> ::= EPSILON

<Term> ::= <Factor> <TermTail>

<TermTail> ::= <MultOp> <Factor> <TermTail>
<TermTail> ::= EPSILON

<Type> ::= 'integer' @makenode~string~type@
<Type> ::= 'float' @makenode~string~type@
<Type> ::= 'string' @makenode~string~type@
<Type> ::= 'id' @makenode~string~type@

<VarDecl> ::= <Type> 'id' @makenode~string~id@ @makenode~list~dimlist@ <ArraySizeRept> ';' @makefamily~3~varDecl@

<VarDeclRep> ::= <VarDecl> @makesibling@ <VarDeclRep>
<VarDeclRep> ::= EPSILON

<Variable> ::= 'id' @makenode~string~id@ <VariableIdnest>

<VariableIdnest> ::= <IndiceRepStart> <VariableIdnestTail>

<VariableIdnestTail> ::= '.' 'id' @makenode~string~id@ <VariableIdnest>
<VariableIdnestTail> ::= EPSILON

<Visibility> ::= 'public'
<Visibility> ::= 'private'
<Visibility> ::= EPSILON

